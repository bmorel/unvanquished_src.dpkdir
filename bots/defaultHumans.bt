selector
{
	decorator return( STATUS_FAILURE )
	{
		sequence
		{
			condition haveWeapon( WP_HBUILD ) && distanceTo( E_GOAL ) <= 100 && percentHealth( E_GOAL ) < 1.0
			action resetStuckTime
		}
	}

	condition ( percentHealth( E_SELF ) <= 0.5 && haveUpgrade( UP_MEDKIT ) )
	{
		action activateUpgrade( UP_MEDKIT )
	}

	behavior unstick

	sequence
	{
		condition alertedToEnemy
		selector
		{
			condition haveWeapon( WP_HBUILD ) && ( !buildingIsDamaged || teamateHasWeapon( WP_HBUILD ) )
			{
				selector
				{
					action equip
					action flee
				}
			}

			decorator timer( 1000 )
			{
				selector
				{
					sequence
					{
						condition ( healScore <= 0.5 && percentHealth( E_GOAL ) > 0.3  && random > 0.3 )
						action heal
					}
				}
			}

			sequence
			{
				//if goal is a close enough enemy building or we're gonna die
				condition ( percentHealth( E_SELF ) < 0.3 || ( distanceTo( E_GOAL ) < 400 && goalType == ET_BUILDABLE ) )
				selector
				{
					condition ( haveUpgrade( UP_FIREBOMB ) )
					{
						action activateUpgrade( UP_FIREBOMB )
					}
					condition ( haveUpgrade( UP_GRENADE ) )
					{
						action activateUpgrade( UP_GRENADE )
					}
				}
			}

			action fight
		}
	}

	selector
	{

		sequence
		{
			condition !haveUpgrade( UP_MEDKIT )
			condition healScore <= 0.25
			action heal
		}
	}

	sequence
	{
		condition !teamateHasWeapon( WP_HBUILD )
		condition buildingIsDamaged
		decorator timer( 50000 )
		{
			selector
			{
				condition !haveWeapon( WP_HBUILD )
				{
					action buy( WP_HBUILD )
				}

				condition haveWeapon( WP_HBUILD )
				{
					action repair
				}
			}
		}
	}

	action equip

	condition baseRushScore > 0.5
	{
		action rush
	}

	action roam
}
