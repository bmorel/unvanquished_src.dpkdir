selector
{
	decorator return( STATUS_FAILURE )
	{
		condition haveWeapon( WP_HBUILD ) && distanceTo( E_GOAL ) <= 100 && percentHealth( E_GOAL ) < 1.0
		{
			action resetStuckTime
		}
	}

	behavior unstick

	condition alertedToEnemy
	{
		selector
		{
			condition haveWeapon( WP_HBUILD ) && ( !buildingIsDamaged || teamateHasWeapon( WP_HBUILD ) )
			{
				selector
				{
					action equip
					action flee
				}
			}

			decorator timer( 1000 )
			{
				condition ( healScore <= 0.5 && percentHealth( E_GOAL ) > 0.3  && random > 0.3 )
				{
					action heal
				}
			}

			action fight
		}
	}

	condition !haveUpgrade( UP_MEDKIT ) && healScore <= 0.25
	{
		action heal
	}

	condition !teamateHasWeapon( WP_HBUILD ) && buildingIsDamaged
	{
		decorator timer( 50000 )
		{
			selector
			{
				condition !haveWeapon( WP_HBUILD )
				{
					action buy( WP_HBUILD )
				}

				action repair
			}
		}
	}

	action equip

	condition baseRushScore > 0.5
	{
		action rush
	}

	action roam
}
