selector
{
	condition ( percentHealth( E_SELF ) <= 0.5 && haveUpgrade( UP_MEDKIT ) )
	{
		action activateUpgrade( UP_MEDKIT )
	}

	sequence
	{
		condition alertedToEnemy
		selector
		{
			decorator timer( 1000 )
			{
				condition haveWeapon( WP_LUCIFER_CANNON ) && distanceTo( E_H_REACTOR ) <= 400
				{
					action buy( WP_PULSE_RIFLE )
				}
			}

			condition haveWeapon( WP_HBUILD ) && ( !buildingIsDamaged || teamateHasWeapon( WP_HBUILD ) )
			{
				selector
				{
					action equip
					action flee
				}
			}

			decorator timer( 1000 )
			{
				selector
				{
					condition ( ( distanceTo( E_H_REACTOR ) <= 100 || distanceTo( E_H_MEDISTAT ) <= 100 ) && isVisible( E_ENEMY ) )
					{
						action fight
					}
					condition ( healScore > 0.5 && percentHealth( E_SELF ) > 0.3 )
					{
						action heal
					}
				}
			}

			sequence
			{
				//if goal is a close enough enemy building or we're gonna die
				condition ( percentHealth( E_SELF ) < 0.3 || ( distanceTo( E_GOAL ) < 400 && goalType == ET_BUILDABLE ) )
				condition ( distanceTo( E_H_REACTOR ) > 400 )
				selector
				{
					condition ( haveUpgrade( UP_FIREBOMB ) )
					{
						action activateUpgrade( UP_FIREBOMB )
					}
					condition ( haveUpgrade( UP_GRENADE ) )
					{
						action activateUpgrade( UP_GRENADE )
					}
				}
			}

			action fight
		}
	}

	selector
	{
		sequence
		{
			condition healScore > 0.50
			action heal
		}
	}

	sequence
	{
		condition !teamateHasWeapon( WP_HBUILD )
		condition buildingIsDamaged
		decorator timer( 50000 )
		{
			selector
			{
				condition !haveWeapon( WP_HBUILD )
				{
					sequence
					{
						decorator return( STATUS_SUCCESS )
						{
							action equip
						}
						action buy( WP_HBUILD )
					}
				}

				condition haveWeapon( WP_HBUILD )
				{
					action repair
				}
			}
		}
	}

	action equip

	condition baseRushScore > 0.5
	{
		action rush
	}

	action roam
}
